/**
 * Commands:
 *   - gradle build
 *   - gradle test
 *   - gradle assemble
 *   - gradle uploadArchives
 *
 * To execute the 'uploadArchives' task, the following properties must be specified
 * in an external 'gradle.properties' file:
 *   - sonatypeUsername
 *   - sonatypePassword
 */

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.johnrengelman.shadow'

group         = 'com.sendgrid'
version       = '4.1.1'
ext.packaging = 'jar'

allprojects {
  apply plugin: 'java'
  sourceCompatibility = 1.7
  targetCompatibility = 1.7
}

if (!hasProperty("sonatypeUsername")) {
  ext.sonatypeUsername = null
  ext.sonatypePassword = null
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.+'
}

buildscript {
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
  }
  repositories {
    jcenter()
  }
}

dependencies {
  compile 'org.apache.httpcomponents:httpcore:4.4.4'
  compile 'org.apache.httpcomponents:httpclient:4.5.2'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile group: 'junit', name: 'junit-dep', version: '4.10'

}

repositories {
  mavenCentral()
}

allprojects {
  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
      }
  }
}

// adds 'with-dependencies' to the shadowJar name
shadowJar {
  classifier 'jar'
  baseName "sendgrid-java-http-client"
  version version
}
jar {
  manifest {
    attributes("Implementation-Title": "http", "Implementation-Version": version)
  }
}

// copy shadowJar to base project directory so they will be in git (and on github for download)
build << {
  copy {
    println "Copying ${shadowJar.archiveName} to $projectDir/repo/com/sendgrid/$version"
    from("$buildDir/libs/${shadowJar.archiveName}")
    into("$projectDir/repo/com/sendgrid/$version")
  }
  copy {
    println "Copying ${shadowJar.archiveName} to $projectDir/repo/com/sendgrid"
    from("$buildDir/libs/${shadowJar.archiveName}")
    into("$projectDir/repo/com/sendgrid")
  }
  tasks.renameSendGridVersionJarToSendGridJar.execute()
}

task renameSendGridVersionJarToSendGridJar {
  doLast {
    file("$projectDir/repo/com/sendgrid/${shadowJar.archiveName}").renameTo(file("$projectDir/repo/com/sendgrid/http-jar.jar"))
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      pom.project {
        name 'http'
        packaging 'jar'
        description 'A simple HTTP client'
        url 'https://github.com/sendgrid/java-http-client'

        scm {
          url 'scm:git@github.com:sendgrid/java-http-client.git'
          connection 'scm:git@github.com:sendgrid/java-http-client.git'
          developerConnection 'scm:git@github.com:sendgrid/java-http-client.git'
        }

        licenses {
          license {
            name 'MIT License'
            url 'http://opensource.org/licenses/MIT'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'thinkingserious'
            name 'Elmer Thomas'
          }
        }
      }
    }
  }
}

artifacts {
  archives shadowJar
  archives jar
  archives javadocJar
  archives sourcesJar
}
